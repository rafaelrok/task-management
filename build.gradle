plugins {
    id 'java'
    id 'org.springframework.boot' version '4.0.0'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'jacoco'
    id 'org.flywaydb.flyway' version '10.20.1'
    id 'checkstyle'
    id 'com.diffplug.spotless' version '6.25.0'
    id 'org.sonarqube' version '6.0.1.5171'
}

group = 'br.com.rafaelvieira'
version = '0.0.1-SNAPSHOT'
description = 'Um sistema RESTful para gerenciar tarefas com:  CRUD completo de tarefas Categorias de tarefas Status de conclusÃ£o Prioridades AtribuiÃ§Ã£o de usuÃ¡rios'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

// ConfiguraÃ§Ã£o da classe principal
springBoot {
    mainClass = 'br.com.rafaelvieira.taskmanagement.TaskManagementApplication'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
    maven {
        url = 'https://repo.maven.apache.org/maven2'
    }
    maven {
        url = 'https://repo.spring.io/milestone'
    }
    maven {
        url = 'https://repo.spring.io/snapshot'
    }
}

ext {
    springdocVersion = '2.8.0'
    testcontainersVersion = '2.0.2'
    testcontainersPostgresqlVersion = '1.21.3'
    testcontainersJunitVersion = '1.21.3'
    assertjVersion = '3.26.3'
    mockitoVersion = '5.14.2'
    junitVersion = '6.0.0'
    flywayVersion = '10.20.1'
}

dependencies {
    // ========== Spring Boot Starters ==========
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-cache'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-websocket'

    // Jackson para JSON (necessÃ¡rio para ObjectMapper)
    implementation 'com.fasterxml.jackson.core:jackson-databind'

    // Configuration Processor - gera metadata para autocomplete no IDE
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    // ========== Database ==========
    runtimeOnly 'org.postgresql:postgresql'
    implementation 'com.zaxxer:HikariCP:5.1.0'

    // ========== Flyway Migration ==========
    implementation "org.flywaydb:flyway-core:${flywayVersion}"
    implementation "org.flywaydb:flyway-database-postgresql:${flywayVersion}"

    // ========== OpenAPI/Swagger Documentation ==========
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:${springdocVersion}"
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-api:${springdocVersion}"

    compileOnly 'org.jetbrains:annotations:26.0.2'
    testCompileOnly 'org.jetbrains:annotations:26.0.2'

    // ========== Lombok ==========
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testCompileOnly 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'

    // ========== JUnit 6 ==========
    testImplementation "org.junit.jupiter:junit-jupiter:${junitVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testImplementation "org.junit.jupiter:junit-jupiter-params:${junitVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // Spring Boot Test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'

    // ========== Testcontainers ==========
    testImplementation "org.testcontainers:testcontainers:${testcontainersVersion}"
    testImplementation "org.testcontainers:postgresql:${testcontainersPostgresqlVersion}"
    testImplementation "org.testcontainers:junit-jupiter:${testcontainersJunitVersion}"

    // ========== AssertJ - Fluent Assertions ==========
    testImplementation "org.assertj:assertj-core:${assertjVersion}"

    // ========== Mockito ==========
    testImplementation "org.mockito:mockito-core:${mockitoVersion}"
    testImplementation "org.mockito:mockito-junit-jupiter:${mockitoVersion}"

    // ========== REST Assured (para testes de API) ==========
    testImplementation 'io.rest-assured:rest-assured:5.5.0'
    testImplementation 'io.rest-assured:json-path:5.5.0'
    testImplementation 'io.rest-assured:xml-path:5.5.0'

    // Opcional: se precisar de suporte adicional ao Jackson nos testes
    testImplementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'

    // ========== H2 Database (para testes) ==========
    testRuntimeOnly 'com.h2database:h2'

    implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity6'
}

// ========== Checkstyle Configuration ==========
checkstyle {
    toolVersion = "12.1.0"
    ignoreFailures = false
    maxWarnings = 0
    maxErrors = 0
}

tasks.withType(Checkstyle).configureEach {
    javaLauncher = javaToolchains.launcherFor {
        languageVersion = JavaLanguageVersion.of(25)
    }

    // Define o arquivo de configuraÃ§Ã£o explicitamente
    configFile = file("${rootProject.projectDir}/config/checkstyle/checkstyle.xml")

    // Define o diretÃ³rio de configuraÃ§Ã£o
    configDirectory = file("${rootProject.projectDir}/config/checkstyle")

    reports {
        xml.required = true
        html.required = true
        html.outputLocation = file("${buildDir}/reports/checkstyle/${name}.html")
    }
}

checkstyleMain {
    source = 'src/main/java'
}

checkstyleTest {
    source = 'src/test/java'
}

// Executar checkstyle antes do build
tasks.named('build') {
    dependsOn 'checkstyleMain', 'checkstyleTest'
}

tasks.register('checkstyleFormat') {
    group = 'verification'
    description = 'Verifica o cÃ³digo com Checkstyle'
    dependsOn 'checkstyleMain', 'checkstyleTest'
}

// Spotless antes do Checkstyle
tasks.named('checkstyleMain') {
    dependsOn 'spotlessApply'
}

tasks.named('checkstyleTest') {
    dependsOn 'spotlessApply'
}

// ========== Spotless (FormataÃ§Ã£o AutomÃ¡tica) ==========
//spotless {
//    java {
//        target 'src/**/*.java', "src/test/**/*.java"
//        googleJavaFormat('1.17.0').aosp().reflowLongStrings()
//        removeUnusedImports()
//        trimTrailingWhitespace()
//        endWithNewline()
//        importOrder()
//        toggleOffOn()
//
//        custom 'Lambda fix', {
//            it.replace('} )', '})').replace('} ,', '},')
//        }
//    }
//
//    format 'misc', {
//        target '**/*.gradle', '**/*.md', '**/.gitignore'
//        trimTrailingWhitespace()
//        indentWithSpaces(4)
//        endWithNewline()
//    }
//}

spotless {
    java {
        target 'src/**/*.java', "src/test/**/*.java"
        googleJavaFormat('1.32.0').aosp().reflowLongStrings()
        removeUnusedImports()
        trimTrailingWhitespace()
        endWithNewline()
        importOrder()
//        formatAnnotations()
        toggleOffOn()

        // MantÃ©m anotaÃ§Ãµes em linhas separadas SEM linhas em branco extras
//        custom 'AnnotationOnSeparateLine', {
//            it.replaceAll(/(\s*)@(\w+)\s+(@\w+)/, '$1@$2\n$1$3')
//                    .replaceAll(/(\s*)@(\w+)\s+(protected|private|public|static|interface)/, '$1@$2\n$1$3')
//                    .replaceAll(/(@\w+\n)\n+(\s*@\w+)/, '$1$2')
//                    .replaceAll(/(@\w+\n)\n+(\s*(?:protected|private|public|static|interface))/, '$1$2')
//        }

        custom 'Lambda fix', {
            it.replace('} )', '})').replace('} ,', '},')
        }
    }

    format 'misc', {
        target '**/*.gradle', '**/*.md', '**/.gitignore'
        trimTrailingWhitespace()
        indentWithSpaces(4)
        endWithNewline()
    }
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs += [
            '-parameters',
            '-Xlint:-options',
            '-Xlint:-deprecation'
    ]
}

// Task para corrigir e verificar cÃ³digo
tasks.register('fixCode') {
    group = 'verification'
    description = 'Aplica formataÃ§Ã£o e verifica cÃ³digo'

    doFirst {
        println 'ðŸ”§ Aplicando correÃ§Ãµes automÃ¡ticas...'
    }

    dependsOn 'spotlessApply'

    doLast {
        println 'âœ… FormataÃ§Ã£o aplicada!'
        println 'ðŸ” Execute: ./gradlew checkstyleMain para verificar'
    }
}

// Task para verificar tudo
// Ajuste em `build.gradle` â€” mover estas configuraÃ§Ãµes para fora do register
tasks.register('verifyAll') {
    group = 'verification'
    description = 'Executa todas as verificaÃ§Ãµes'
    dependsOn 'clean', 'spotlessApply', 'checkstyleMain', 'checkstyleTest', 'test', 'jacocoTestReport'
}

// Configura a ordem entre tasks usando providers no nÃ­vel do projeto
tasks.named('spotlessApply') {
    mustRunAfter 'clean'
}
tasks.named('checkstyleMain') {
    mustRunAfter 'spotlessApply'
}
tasks.named('checkstyleTest') {
    mustRunAfter 'checkstyleMain'
}
tasks.named('test') {
    mustRunAfter 'checkstyleTest'
}
tasks.named('jacocoTestReport') {
    mustRunAfter 'test'
}

// Task para verificar formataÃ§Ã£o
tasks.register('checkFormat') {
    dependsOn 'spotlessCheck'
    description = 'Check code formatting'
    group = 'verification'
}

// ========== ConfiguraÃ§Ã£o de Testes ==========
tasks.named('test') {
    useJUnitPlatform()

    // Argumentos JVM para Java 25
    jvmArgs = [
            '--enable-preview',
            '--add-opens', 'java.base/java.lang=ALL-UNNAMED',
            '--add-opens', 'java.base/java.util=ALL-UNNAMED',
            '--add-opens', 'java.base/java.time=ALL-UNNAMED'
    ]

    // ADICIONE ESTAS VARIÃVEIS DE AMBIENTE
    environment 'DOCKER_HOST', 'npipe:////./pipe/docker_engine'

    // ConfiguraÃ§Ã£o de logging dos testes
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat = "full"
        showStandardStreams = true
        showCauses = true
        showStackTraces = true
    }

    // ConfiguraÃ§Ã£o de memÃ³ria
    maxHeapSize = '2G'
    minHeapSize = '512M'

    // ExecuÃ§Ã£o paralela (aproveita melhor os recursos)
    maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1

    // Finaliza a JVM apÃ³s os testes
    forkEvery = 100
}

// ========== Jacoco Test Coverage ==========
jacoco {
    toolVersion = "0.8.13"
}

tasks.named('jacocoTestReport') {
    dependsOn test

    reports {
        xml.required = true
        html.required = true
        csv.required = false

        html.outputLocation = layout.buildDirectory.dir('reports/jacoco')
    }

    classDirectories.setFrom(files(classDirectories.files.collect {
        fileTree(dir: it, exclude: [
                '**/config/**',
                '**/records/**',
                '**/exception/**',
                '**/*Application.class'
        ])
    }))
}

tasks.named('jacocoTestCoverageVerification') {
    dependsOn jacocoTestReport

    violationRules {
        rule {
            limit {
                minimum = 0.80
            }
        }
    }
}

// ========== Build Task Customizado ==========
tasks.named('build') {
    dependsOn jacocoTestReport
}

// ========== Clean Task ==========
tasks.named('clean') {
    delete 'out'
    delete 'logs'
}

// ========== Custom Tasks ==========

// Task para executar apenas testes unitÃ¡rios
tasks.register('unitTest', Test) {
    useJUnitPlatform {
        includeTags 'unit'
    }
    description = 'Run unit tests'
    group = 'verification'
}

// Task para executar apenas testes de integraÃ§Ã£o
tasks.register('integrationTest', Test) {
    useJUnitPlatform {
        includeTags 'integration'
    }
    description = 'Run integration tests'
    group = 'verification'

    shouldRunAfter(unitTest)
}

// Task para verificar dependÃªncias desatualizadas
tasks.register('checkDependencyUpdates') {
    doLast {
        println '========================================='
        println 'Checking for dependency updates...'
        println '========================================='
    }
}

// ========== Gradle Wrapper ==========
tasks.named('wrapper') {
    gradleVersion = '9.1.0'
    distributionType = Wrapper.DistributionType.ALL
}

// Exclude deprecated Flyway migrations to avoid version conflicts
tasks.named('processResources') {
    filesMatching('db/migration/V8__add_pomodoro_and_description_length.sql') { details ->
        details.exclude()
    }
    filesMatching('db/migration/V9__add_status_in_pause.sql') { details ->
        details.exclude()
    }
}
